import PartitionsLeanblueprint.ModularFormDefs
import PartitionsLeanblueprint.ModuloDefs2
import PartitionsLeanblueprint.BasicOperators
import Mathlib.Logic.Function.Iterate
import Mathlib.Data.Nat.Prime.Defs
import Mathlib.GroupTheory.Perm.Centralizer
import Mathlib.Data.Nat.Prime.Factorial

/- This file states and proves some basic theorems, some of which are found
in the introduction of the paper -/

open ModularFormDefs Integer Modulo2

noncomputable section

variable {â„“ n : â„•} [NeZero â„“] [Fact (Nat.Prime â„“)]
variable {k j : ZMod (â„“-1)}
variable {a b : ModularFormMod â„“ k}

open Nat Finset ZMod Finset.Nat

@[simp]
lemma flt {p : â„•} {n : ZMod p} [Fact (Nat.Prime p)] : n ^ p = n := pow_card n

@[simp]
lemma flt2 {p : â„•} {n : ZMod p} [Fact (Nat.Prime p)] : n ^ (p - 1) = if n â‰  0 then 1 else 0 :=
  pow_card_sub_one n


section Pow_Prime

-- Declares that two functions, which can be thought of as tuples, are permutations of one another
def perm_equiv {n : â„•} (a b : Fin n â†’ â„•) :=
  âˆƒ c : Equiv.Perm (Fin n), a = b âˆ˜ c

lemma perm_equiv_refl {n : â„•} (a : Fin n â†’ â„•) : perm_equiv a a :=
  âŸ¨1, by rw [Equiv.Perm.coe_one]; rflâŸ©

theorem perm_equiv_symm {n} {a b : Fin n â†’ â„•} : perm_equiv a b â†’ perm_equiv b a := by
  rintro âŸ¨c, hcâŸ©; use câ»Â¹; rw[hc]; ext x; simp

theorem perm_equiv_trans {n} {a b c : Fin n â†’ â„•} : perm_equiv a b â†’ perm_equiv b c â†’ perm_equiv a c := by
  rintro âŸ¨Ïƒ, hÏƒâŸ© âŸ¨Ï„, hÏ„âŸ©
  refine âŸ¨Ïƒ.trans Ï„, ?_âŸ©
  ext i
  have : b (Ïƒ i) = (c âˆ˜ Ï„) (Ïƒ i) := by simpa using congrArg (fun f => f (Ïƒ i)) hÏ„
  simpa [Function.comp, hÏƒ] using this


theorem perm_equiv_const {n} {a b: Fin n â†’ â„•} (aconst : âˆ€ i j, a i = a j)
    (h : perm_equiv a b) : a = b := by
  obtain âŸ¨c,rflâŸ© := h
  ext i
  have := aconst i (c.symm i)
  simp [Equiv.apply_symm_apply] at this
  exact this

lemma sum_eq_of_perm_equiv {n} {a b : Fin n â†’ â„•} (h : perm_equiv a b) :
    âˆ‘ i, a i = âˆ‘ i, b i := by
  obtain âŸ¨c,hc,rflâŸ© := h
  exact Equiv.sum_comp c b

-- the equivalence class of functions that are permutations of x
def perm_setoid : Setoid ( Fin n â†’ â„• ) where
  r := perm_equiv
  iseqv := âŸ¨perm_equiv_refl, perm_equiv_symm, perm_equiv_transâŸ©


def orbit_finset {k} (x : Fin k â†’ â„•) : Finset (Fin k â†’ â„•) :=
  univ.image (fun c : Equiv.Perm (Fin k) â†¦ x âˆ˜ c)

lemma orbit_equiv {k} {x y: Fin k â†’ â„•} : y âˆˆ orbit_finset x â†” perm_equiv x y := by
  unfold perm_equiv orbit_finset; constructor <;> intro h <;>
  simp_all only [mem_image, mem_univ, true_and]
  obtain âŸ¨c, rflâŸ© := h
  use câ»Â¹; ext; simp
  obtain âŸ¨c, rflâŸ© := h
  use câ»Â¹; ext; simp


lemma perm_of_orbit {k} {x b : Fin k â†’ â„•} (h : b âˆˆ orbit_finset x) : perm_equiv x b := by
  rcases Finset.mem_image.mp h with âŸ¨c, _, rflâŸ©
  use câ»Â¹; ext i; simp

lemma orbit_eq_tuple {k n} {x : Fin k â†’ â„•} (h : x âˆˆ antidiagonalTuple k n) :
    orbit_finset x = {b âˆˆ antidiagonalTuple k n | perm_equiv x b} := by
  ext b; constructor <;> intro hb
  apply mem_filter.mpr; constructor
  rcases Finset.mem_image.mp hb with âŸ¨c, _, rflâŸ©
  apply mem_antidiagonalTuple.mpr; trans âˆ‘ i, x i
  exact Fintype.sum_equiv c (x âˆ˜ â‡‘c) x (congrFun rfl)
  exact mem_antidiagonalTuple.mp h
  exact perm_of_orbit hb
  have : perm_equiv x b := by simp_all only [mem_filter]
  obtain âŸ¨c,rflâŸ© := this
  apply Finset.mem_image.mpr
  use câ»Â¹; constructor
  simp_all only [mem_filter, mem_univ]
  ext i; simp


def subtype_univ_equiv {Î± : Type*} [Fintype Î±] : ({a : Î± // a âˆˆ (Finset.univ : Finset Î±)}) â‰ƒ Î± where
    toFun := Subtype.val
    invFun := fun a => âŸ¨a, mem_univ aâŸ©
    left_inv := fun âŸ¨_, _âŸ© => rfl
    right_inv := fun _ => rfl


-- If the tuple x is not constant, ie [k,k,k, ..], then
-- â„“ | (# of permutations of x âˆˆ antidiagonalTuple â„“ n)
lemma non_diag_vanish {k n : â„•} {x : Fin k â†’ â„•} [Fact (Nat.Prime k)] (h : Â¬ âˆ€ i j, x i = x j)  :
    k âˆ£ #{ b âˆˆ antidiagonalTuple k n | perm_equiv x b } := by
  simp_all only [not_forall]
  obtain âŸ¨w, hâŸ© := h
  obtain âŸ¨u, hâŸ© := h

  by_cases xiT : x âˆˆ antidiagonalTuple k n

  { -- x âˆˆ antidiagonalTuple k n â†’ card = (k)! / âˆ m âˆˆ univ.image x, (#{n | x n = m})!
    have perm_in_set {b} (h : perm_equiv x b) : b âˆˆ { b âˆˆ antidiagonalTuple k n | perm_equiv x b } := by
      refine mem_filter.mpr âŸ¨?_, hâŸ©
      apply mem_antidiagonalTuple.mpr
      trans âˆ‘ i, x i
      exact sum_eq_of_perm_equiv (perm_equiv_symm h)
      apply mem_antidiagonalTuple.mp xiT


    rw[â† orbit_eq_tuple xiT]

    -- the stabilizer : the set of permutations which leave x unchanged
    let Stab : Finset (Equiv.Perm (Fin k)) := {c : Equiv.Perm (Fin k) | x âˆ˜ c = x}

    -- the orbit stabilizer theorem, stated in finset language
    have decomp : #(Finset.univ : Finset (Equiv.Perm (Fin k))) = #(orbit_finset x) * #Stab := by
      {
      let f : Equiv.Perm (Fin k) â†’ (Fin k â†’ â„•) := fun g â†¦ x âˆ˜ g
      calc
        _  = âˆ‘ y âˆˆ Finset.univ.image f, ((Finset.univ.filter (fun g â†¦ f g = y)).card) := by
          exact card_eq_sum_card_image f Finset.univ
        _ = âˆ‘ y âˆˆ orbit_finset x, #Stab := by
          refine Finset.sum_congr rfl ?_
          intro y hy
          simp[f,Stab]
          have hyy := orbit_equiv.1 hy
          obtain âŸ¨d, rflâŸ© := hyy
          have {c : Equiv.Perm (Fin k)} : (y âˆ˜ â‡‘d) âˆ˜ â‡‘c = y âˆ˜ â‡‘d â†” ((y âˆ˜ â‡‘d) âˆ˜ â‡‘c) âˆ˜ â‡‘dâ»Â¹ = y := by
            constructor <;> intro h; rw[h]; ext; simp
            nth_rw 2[â† h]; ext; simp

          simp only [this]

          have im_eq :  Finset.image (fun g => g * d) { g : Equiv.Perm (Fin k) | (y âˆ˜ d) âˆ˜ g = y } =
              ({ c : Equiv.Perm (Fin k) | ((y âˆ˜ d) âˆ˜ c) âˆ˜ â‡‘dâ»Â¹ = y } : Finset (Equiv.Perm (Fin k))) := by
            ext c
            constructor
            intro h
            simp_all; nth_rw 2[â† h]; ext; simp
            intro h
            simp_all; nth_rw 2[â† h]; ext; simp

          rw[â† im_eq]
          refine Eq.symm (Finset.card_image_of_injOn ?_)
          intro x hx z hz
          simp_all

        _ = #(orbit_finset x) * #Stab := sum_const_nat Î» _ â†¦ congrFun rfl
      }

    have card_univ : #(Finset.univ : Finset (Equiv.Perm (Fin k))) = (k)! := by
      rw [Finset.card_univ, Fintype.card_perm, Fintype.card_fin]

    have decomp_div : #(orbit_finset x) = #(univ : Finset (Equiv.Perm (Fin k))) / #Stab := by
      refine Nat.eq_div_of_mul_eq_left ?_ (id (Eq.symm decomp))
      unfold Stab; apply Finset.card_ne_zero.mpr
      use 1; simp

    rw[decomp_div, card_univ]

    have Stabpos : #Stab â‰  0 := Finset.card_ne_zero.mpr âŸ¨1, by simp [Stab]âŸ©
    have kPrime : Nat.Prime k := Fact.out
    have kn0 : k â‰  0 := Ne.symm (NeZero.ne' k)

    suffices getStabbed : Â¬ k âˆ£ #Stab by
      have unStabbed : #Stab âˆ£ (k)! := by
        use #(orbit_finset x); rw[mul_comm, â† decomp, card_univ]
      have : k âˆ£ k ! := dvd_factorial (zero_lt_of_ne_zero kn0) (le_refl k)
      obtain âŸ¨t, htâŸ© := unStabbed
      have hmul : k âˆ£ (Stab.card : â„•) * t := by rw[â† ht]; exact this

      rcases (Nat.Prime.dvd_mul kPrime).1 hmul with h|h
      contradiction
      rw[ht]; rw [mul_div_cancel_leftâ‚€ t Stabpos]
      exact h


    clear! n perm_in_set decomp card_univ decomp_div


    -- Final Step : Â¬ k âˆ£ #Stab

    intro divStab

    have Stab_pi : #Stab = âˆ m âˆˆ univ.image x, (#{n | x n = m})! := by

      { -- rewriting to be able to apply DomMulAct.stabilizer_card

        clear! w u kPrime kn0 divStab Stabpos

        let y : Fin k â†’ {m // m âˆˆ image x univ} :=
          fun n â†¦ âŸ¨x n, mem_image_of_mem x (mem_univ n)âŸ©

        unfold Stab

        let Stab' := {g : Equiv.Perm (Fin k) // y âˆ˜ g = y}


        have Stabsyou : #Stab = Fintype.card Stab' := by
          unfold Stab' Stab
          apply card_eq_of_equiv_fintype
          apply Equiv.subtypeEquivProp
          ext c; constructor <;> intro h
          simp at h
          funext n; simp[y]
          trans (x âˆ˜ c) n
          simp[h]; rw[h]
          simp_all[y]
          ext n
          exact congrArg Subtype.val (congrFun h n)

        have rrw (m : â„•) : #{n | x n = m} = Fintype.card { a // y a = m } := by
          apply card_eq_of_equiv_fintype
          apply Equiv.subtypeEquivProp
          ext n; constructor <;> intro h <;> simp_all[y]

        rw[Stabsyou]
        simp_rw [rrw]
        unfold Stab'

        have rrr : âˆ m âˆˆ image x univ, (Fintype.card { a // x a = m })! =
            âˆ m : {m // m âˆˆ image x univ}, (Fintype.card { a // y a = m })! := by

          let f (Î± : Type 0) [Fintype Î±] : â„• := Nat.factorial (Fintype.card Î±)

          have eq3 {m}: (Fintype.card { a // x a = m })! = f { a // x a = m } := by simp[f]
          have eq4 {m}: (Fintype.card { a // y a = m })! = f { a // y a = m } := by simp[f]

          have eq5 {m}: f { a // x a = m } = f { a // y a = m } := by congr

          let h (m : â„•) := f { a // y a = m }

          have eq6 {m} : f { a // y a = m } = h m := by
            obtain âŸ¨val, propertyâŸ© := m
            simp_all only [Subtype.mk.injEq, h, f, y]

          simp_rw[eq3,eq4,eq5]
          simp; symm
          simp_rw[eq6]

          rw [prod_attach]

        rw[rrr]

        apply DomMulAct.stabilizer_card
      }

    have : âˆ€ m âˆˆ univ.image x, Â¬ k âˆ£ (#{n | x n = m})! := by
      intro m hm
      suffices conned : #{n | x n = m} < k by
        have necon0 : #{n | x n = m} â‰  0 := (fiber_card_ne_zero_iff_mem_image univ x m).mpr hm
        contrapose! conned
        apply (Nat.Prime.dvd_factorial kPrime).1 conned

      by_cases xwm : x w = m

      have xum : u âˆ‰ ({n | x n = m} : Finset (Fin k)) := by simp; rw[â† xwm]; exact Î» a â†¦ h (id (Eq.symm a))
      contrapose! xum
      have : ({n | x n = m} : Finset (Fin k)) = univ := by
        apply Finset.eq_univ_of_card
        trans k
        exact le_antisymm (card_finset_fin_le {n | x n = m}) xum
        exact Eq.symm (Fintype.card_fin k)
      rw[this]; exact mem_univ u

      have xum : w âˆ‰ ({n | x n = m} : Finset (Fin k)) := by
        contrapose! xwm; simp_all only [ne_eq, mem_univ, true_and, mem_filter]
      contrapose! xum
      have : ({n | x n = m} : Finset (Fin k)) = univ := by
        apply Finset.eq_univ_of_card
        trans k
        exact le_antisymm (card_finset_fin_le {n | x n = m}) xum
        exact Eq.symm (Fintype.card_fin k)
      rw[this]; exact mem_univ w

    contrapose! this; clear this
    rw [Stab_pi] at divStab

    have kPrime' : _root_.Prime k := prime_iff.mp kPrime

    -- Convert the finset product to a list product
    have : âˆ m âˆˆ (image x univ), (#{n | x n = m})! = List.prod ((image x univ).toList.map (Î» m => (#{n | x n = m})!)) :=
      Eq.symm (prod_map_toList (image x univ) Î» m â†¦ (#{n | x n = m})!)
    rw [this] at divStab
    obtain âŸ¨a, ha, hkaâŸ© := (Prime.dvd_prod_iff kPrime').mp divStab
    rcases List.mem_map.mp ha with âŸ¨m, hm, rflâŸ©
    use m
    exact âŸ¨Finset.mem_toList.mp hm, hkaâŸ©

  }

  { -- x âˆ‰ antidiagonalTuple k n â†’ card = 0
    use 0; simp; apply filter_false_of_mem
    intro b hb; contrapose! xiT
    apply mem_antidiagonalTuple.mpr
    trans âˆ‘ i, b i
    exact sum_eq_of_perm_equiv xiT
    exact mem_antidiagonalTuple.mp hb
  }

-- the products over two permutationally equivalent functions are equal
lemma Pi_eq_of_perm_equiv {n : â„•} {a : â„• â†’ ZMod n} {x y : Fin n â†’ â„•} (hxy : perm_equiv x y) :
    âˆ z, a (y z) = âˆ z, a (x z) := by
  symm; unfold perm_equiv at hxy
  obtain âŸ¨c, hcâŸ© := hxy
  simp[hc]; exact Fintype.prod_equiv c (fun x â†¦ a (y (c x))) (fun x â†¦ a (y x)) (congrFun rfl)

-- every element of a non-diagonal tuple is non-constant
lemma non_const_of_tuple_non_diag {k n : â„•} (h : Â¬ k âˆ£ n) (x : Fin k â†’ â„•) (hx : x âˆˆ antidiagonalTuple k n ) :
    (Â¬ âˆ€ i j, x i = x j) := by
  contrapose! hx
  suffices âˆ‘ i, x i â‰  n by
    contrapose! this; exact mem_antidiagonalTuple.mp this
  contrapose! h

  by_cases k0 : k = 0
  have : âˆ‘ i, x i = 0 := by
    subst k0 h
    simp_all only [IsEmpty.forall_iff, implies_true, univ_eq_empty, sum_empty]
  rw[k0]; apply Nat.zero_dvd.2; rw[â† h, this]

  have : âˆƒ m, k = m + 1 := exists_eq_succ_of_ne_zero k0
  obtain âŸ¨m,hmâŸ© := this
  subst hm; clear k0
  have h' : âˆ‘ i, x i = (m + 1) * x 0 := by
    trans âˆ‘ i : Fin (m + 1), x 0
    exact sum_congr rfl (Î» x _ â†¦ hx x 0)
    apply Fin.sum_const
  use x 0; rw[â† h, h']

-- every non-diagonal element of a diagonal tuple is non-constant
lemma non_const_of_tuple_diag {k n : â„•} (x : Fin k â†’ â„•) (kn0 : k â‰  0) (hx : x âˆˆ antidiagonalTuple k (k * n) \ {fun _ â†¦ n}) :
    (Â¬ âˆ€ i j, x i = x j) := by
  contrapose! hx
  have hmk : âˆƒ m, k = m + 1 := exists_eq_succ_of_ne_zero kn0
  obtain âŸ¨m,hm,rflâŸ© := hmk
  by_contra! h
  have hnconst : x â‰  fun x â†¦ n := by
    contrapose! h; simp; exact Î» _ â†¦ h
  have : âˆ‘ i, x i = (m + 1) * n := by apply mem_antidiagonalTuple.mp; simp_all only [ne_eq, Nat.add_eq_zero,
    one_ne_zero, and_false, not_false_eq_true, mem_sdiff, mem_singleton, and_true]
  have const : âˆ‘ i, x i = (m + 1) * x 0 := by
    specialize hx 0
    trans âˆ‘ _ : Fin (m + 1), x 0
    exact Eq.symm (Fintype.sum_congr (fun a â†¦ x 0) x hx)
    apply Fin.sum_const
  contrapose! hnconst
  funext i
  calc
   x i = x 0 := hx i 0
   x 0 = n := by
    have : (m + 1) * n = (m + 1) * x 0 := by rw[â† this, â† const]
    exact (Nat.mul_right_inj kn0).mp (id (Eq.symm this))

@[simp]
theorem Pow_Prime {n : â„•} {a : ModularFormMod â„“ k} : (a ** â„“) n = if â„“ âˆ£ n then (a (n / â„“)) else 0 := by

  by_cases h : â„“ âˆ£ n

  { -- antidiagonalTuple is diagonal (ie â„“ âˆ£ len) â†’ only diagonal tuple remains
    simp [pow_apply,h]
    obtain âŸ¨k,rflâŸ© := h
    have la : â„“ * k / â„“ = k := by
      refine Eq.symm (Nat.eq_div_of_mul_eq_right ?_ rfl); exact Ne.symm (NeZero.ne' â„“)
    rw[la]
    have vanish : âˆ‘ x âˆˆ antidiagonalTuple â„“ (â„“ * k) \ {fun _ â†¦ k}, âˆ y, a (x y) = 0 := by
      {
        let Tup := antidiagonalTuple â„“ (â„“ * k) \ {fun _ â†¦ k}

        have blister : âˆ€ x âˆˆ Tup, â„“ âˆ£ #{ b âˆˆ antidiagonalTuple â„“ (â„“ * k) | perm_equiv x b } :=
          Î» x hx â†¦ non_diag_vanish (non_const_of_tuple_diag x (Ne.symm (NeZero.ne' â„“)) hx)

        have Step (x : Fin â„“ â†’ â„•) : âˆ‘ z âˆˆ {b âˆˆ Tup | perm_equiv x b}, âˆ y, a (z y) = 0 := by
          by_cases hx : x âˆˆ antidiagonalTuple â„“ (â„“ * k)
          {
            by_cases xconst : x = â†‘k
            {
              have empty : {z âˆˆ Tup | perm_equiv x z} = âˆ… := by
                refine filter_false_of_mem ?_; intro z hz
                have zconst : z â‰  â†‘k := by
                  subst xconst
                  simp_all only [mem_sdiff, mem_singleton, and_imp, ne_eq, Tup]
                  exact hz.2
                intro hxz
                apply perm_equiv_const at hxz
                rw[â† hxz, xconst] at zconst
                contradiction
                intros; simp[xconst]
              rw[empty]; rfl
            }

            {
              have Tup_eq : {b âˆˆ Tup | perm_equiv x b} = {b âˆˆ antidiagonalTuple â„“ (â„“ * k) | perm_equiv x b} := by
                {
                  apply Subset.antisymm_iff.mpr; constructor <;> intro c hc
                  have ss : Tup âŠ† antidiagonalTuple â„“ (â„“ * k) := sdiff_subset
                  refine mem_filter.mpr ?_; constructor
                  have : c âˆˆ Tup := mem_of_mem_filter c hc
                  exact ss this
                  simp_all only [mem_sdiff, mem_singleton, and_imp, mem_filter, sdiff_subset, Tup]

                  by_cases cc : c = â†‘k
                  have hxc : perm_equiv x c := by
                    subst cc; simp_all only [mem_sdiff, mem_singleton, and_imp, mem_filter, Tup]

                  have : âˆ€ i j, c i = c j := by intros; simp[cc]
                  have cex : c = x := perm_equiv_const this (perm_equiv_symm hxc)
                  rw[cc] at cex; exact False.elim (xconst (id (Eq.symm cex)))

                  refine mem_filter.mpr ?_; constructor
                  have ciT : c âˆˆ antidiagonalTuple â„“ (â„“ * k) := mem_of_mem_filter c hc
                  exact mem_sdiff.mpr âŸ¨ciT, notMem_singleton.mpr ccâŸ©
                  simp_all only [mem_sdiff, mem_singleton, and_imp, mem_filter, Tup]
                }

              have hxx : x âˆˆ Tup := by
                simp_all only [mem_sdiff, mem_singleton, true_and, Tup]
                exact xconst

              have pi_eq : âˆ€ z âˆˆ {b âˆˆ antidiagonalTuple â„“ (â„“ * k) | perm_equiv x b}, âˆ y, a (z y) = âˆ y, a (x y) := by
                intro z hz
                have hxz : perm_equiv x z := by simp_all only [mem_filter]
                exact Pi_eq_of_perm_equiv hxz

              rw[Tup_eq]

              calc
                _ = âˆ‘ _ âˆˆ {b âˆˆ antidiagonalTuple â„“ (â„“ * k) | perm_equiv x b}, âˆ y, a (x y) := sum_congr rfl pi_eq
                _ = #{b âˆˆ antidiagonalTuple â„“ (â„“ * k) | perm_equiv x b} * âˆ y, a (x y) := by simp
                _ = 0 * âˆ y, a (x y) := by
                  congr; exact (natCast_zmod_eq_zero_iff_dvd _ _).2 (blister x hxx)
                _ = 0 := zero_mul _
            }
          }

          {
            have empty : {b âˆˆ Tup | perm_equiv x b} = âˆ… := by
              refine filter_false_of_mem ?_
              intro b hb; contrapose! hx
              refine mem_antidiagonalTuple.mpr ?_
              trans âˆ‘ i, b i
              exact sum_eq_of_perm_equiv hx
              refine mem_antidiagonalTuple.mp ?_
              simp_all only [mem_sdiff, mem_singleton, and_imp, Tup]
            rw[empty]; rfl
          }

        -- The Set-theoretic quotient of Tup by permutational equvalence
        let Qfin := (Tup).image (Quotient.mk (perm_setoid))

        -- Rewrite as a sum over Qfin so that we can apply Step
        calc
      _  = âˆ‘ q âˆˆ Qfin, âˆ‘ z âˆˆ {x âˆˆ Tup | âŸ¦xâŸ§ = q}, âˆ y, a (z y) := by
          apply sum_partition
      _ = âˆ‘ q âˆˆ Qfin, 0 := by
          apply sum_congr rfl
          intro q hq
          rcases Quot.exists_rep q with âŸ¨x, rflâŸ©
          trans âˆ‘ z âˆˆ Tup with perm_equiv x z, âˆ y, a (z y)
          congr; funext z; apply propext
          have : âŸ¦zâŸ§ = Quot.mk (â‡‘perm_setoid) x â†” perm_equiv z x := Quotient.eq
          simp[this]; constructor <;> exact perm_equiv_symm
          exact Step x
      _ = 0 := sum_const_zero

      }

    calc
      _ = ( âˆ‘ x âˆˆ antidiagonalTuple â„“ (â„“ * k) \ {fun _ â†¦ k}, âˆ y, a (x y) ) +
          ( âˆ‘ x âˆˆ {fun _ â†¦ k}, âˆ y : Fin â„“, a (x y) ) := by
        apply Eq.symm (sum_sdiff ?_); apply singleton_subset_iff.2
        apply mem_antidiagonalTuple.mpr; simp[sum_const]

      _ = 0 + âˆ‘ x âˆˆ {fun _ â†¦ k}, âˆ y : Fin â„“, a (x y) := by congr
      _ = âˆ _ : Fin â„“, a k := by simp
      _ = (a k) ^ â„“ := Fin.prod_const â„“ (a k)
      _ = a k := flt
  }


  { -- antidiagonalTuple is not diagonal â†’ no tuples remain
    simp[pow_apply,h]

    have blister : âˆ€ x âˆˆ antidiagonalTuple â„“ n, â„“ âˆ£ #{ b âˆˆ antidiagonalTuple â„“ n | perm_equiv x b } :=
      Î» x hx â†¦ non_diag_vanish (non_const_of_tuple_non_diag h x hx)

    have Step : âˆ€ x : (Fin â„“ â†’ â„•), âˆ‘ z âˆˆ {b âˆˆ antidiagonalTuple â„“ n | perm_equiv x b}, âˆ y, a (z y) = 0 := by
      {
        intro x
        by_cases hx : x âˆˆ antidiagonalTuple â„“ n
        {
          have pi_eq : âˆ€ z âˆˆ {b âˆˆ antidiagonalTuple â„“ n | perm_equiv x b}, âˆ y, a (z y) = âˆ y, a (x y) := by
            intro z hz
            have hxz : perm_equiv x z := by simp_all only [mem_filter]
            exact Pi_eq_of_perm_equiv hxz
          calc
            _ = âˆ‘ _ âˆˆ {b âˆˆ antidiagonalTuple â„“ n | perm_equiv x b}, âˆ y, a (x y) := sum_congr rfl pi_eq
            _ = #{b âˆˆ antidiagonalTuple â„“ n | perm_equiv x b} * âˆ y, a (x y) := by simp
            _ = 0 * âˆ y, a (x y) := by
              congr; exact (natCast_zmod_eq_zero_iff_dvd _ _).2 (blister x hx)
            _ = 0 := zero_mul _
        }

        {
          have empty : {b âˆˆ antidiagonalTuple â„“ n | perm_equiv x b} = âˆ… := by
            refine filter_false_of_mem ?_
            intro b hb; contrapose! hx
            refine mem_antidiagonalTuple.mpr ?_
            trans âˆ‘ i, b i
            exact sum_eq_of_perm_equiv hx
            exact mem_antidiagonalTuple.mp hb
          rw[empty]; rfl
        }
      }

    let Qfin := (antidiagonalTuple â„“ n).image (Quotient.mk (perm_setoid))

    calc
      _  = âˆ‘ q âˆˆ Qfin, âˆ‘ z âˆˆ {x âˆˆ antidiagonalTuple â„“ n | âŸ¦xâŸ§ = q}, âˆ y, a (z y) := by
          apply sum_partition
      _ = âˆ‘ q âˆˆ Qfin, 0 := by
          apply sum_congr rfl
          intro q hq
          rcases Quot.exists_rep q with âŸ¨x, rflâŸ©
          trans âˆ‘ z âˆˆ antidiagonalTuple â„“ n with perm_equiv x z, âˆ y, a (z y)
          congr; funext z; apply propext
          have : âŸ¦zâŸ§ = Quot.mk (â‡‘perm_setoid) x â†” perm_equiv z x := Quotient.eq
          simp[this]; constructor <;> exact perm_equiv_symm
          exact Step x
      _ = 0 := sum_const_zero
  }


end Pow_Prime


theorem U_pow_l_eq_self_sub_Theta_pow_l_minus_one' {a : ModularFormMod â„“ k} :
  (a|ğ“¤) ** â„“ == (a -l (Î˜^[â„“ - 1] a)) (by simp) := by
  intro n; simp; symm; calc
    _ = if (n : ZMod â„“) = 0 then a n else 0 := by
      by_cases h : (n : ZMod â„“) = 0 <;> simp[h]
    _ = _ := by
      by_cases h : (n : ZMod â„“) = 0
      have  h' : â„“ âˆ£ n := (natCast_zmod_eq_zero_iff_dvd n â„“).mp h
      simp[h,h']; congr
      rw [Nat.mul_div_cancel_left' h']
      have h': Â¬ â„“ âˆ£ n := by contrapose! h; exact (natCast_zmod_eq_zero_iff_dvd n â„“).mpr h
      simp[h,h']



def thing (a : ModularFormMod â„“ k) := a - (Mcongr (by simp) (Î˜^[â„“ - 1] a))

lemma k_l : k * â„“ = k := by
  calc
    k * â„“ = k * (â„“ - 1 + 1) := by simp
    _ = k * (â„“ - 1) + k := by ring
    _ = k * 0 + k := by
      congr; sorry
    _ = k := by simp only [mul_zero, zero_add]

theorem U_pow_l_eq_self_sub_Theta_pow_l_minus_one {a : ModularFormMod â„“ k} :
(Mcongr (k_l) ((a|ğ“¤) ** â„“)) = thing a := by
  ext n; simp[thing]
  symm; calc
    _ = if (n : ZMod â„“) = 0 then a n else 0 := by
      by_cases h : (n : ZMod â„“) = 0 <;> simp[h]
    _ = _ := by
      by_cases h : (n : ZMod â„“) = 0
      have h' : â„“ âˆ£ n := (ZMod.natCast_zmod_eq_zero_iff_dvd n â„“).mp h
      simp[h,h']; congr
      rw [Nat.mul_div_cancel_left' h']
      have h': Â¬ â„“ âˆ£ n := by contrapose! h; exact (ZMod.natCast_zmod_eq_zero_iff_dvd n â„“).mpr h
      simp[h,h']



theorem Filtration_Log {i : â„•} [NeZero (â„“ - 1)] : ğ”€ (a ** i) = i * ğ”€ a := sorry



def Î´ (â„“ : â„•) : â„¤ := (â„“^2 - 1) / 24
-- or Î´â„“ : â„¤ := (â„“^2 - 1) / 24
